AC_INIT(gtkcairo/gtkcairo.h)

dnl ===========================================================================

# Package version number, (as distinct from shared library version)
GTKCAIRO_VERSION=0.3

# libtool shared library version

# Increment if the interface has additions, changes, removals.
LT_CURRENT=2

# Increment any time the source changes; set to
# 0 if you increment CURRENT
LT_REVISION=1

# Increment if any interfaces have been added; set to 0
# if any interfaces have been removed. removal has
# precedence over adding, so set to 0 if both happened.
LT_AGE=0

VERSION_INFO="$LT_CURRENT:$LT_REVISION:$LT_AGE"
AC_SUBST(VERSION_INFO)

dnl ===========================================================================

AM_INIT_AUTOMAKE(gtkcairo, $GTKCAIRO_VERSION, [dist-bzip2])
AM_CONFIG_HEADER(config.h)

AM_MAINTAINER_MODE

AC_PROG_CC
AC_PROG_CPP
AM_PROG_LIBTOOL
AC_STDC_HEADERS

dnl ===========================================================================

AC_PATH_XTRA

dnl ===========================================================================
PKG_CHECK_MODULES(GTKCAIRO, gtk+-2.0 cairo >= 0.6.0)
AC_SUBST(GTKCAIRO_CFLAGS)
AC_SUBST(GTKCAIRO_LIBS)

dnl ===========================================================================

AC_ARG_ENABLE(gl,
               [  --enable-gl            Enable OpenGL support (glitz)],
               [use_gl=$enableval],  [use_gl=yes])

if test "x$use_gl" = "xyes"; then
  PKG_CHECK_MODULES(GL, glitz-glx >= 0.5.0, [
  GTKCAIRO_CFLAGS="$GTKCAIRO_CFLAGS $GL_CFLAGS -DUSE_GL" 
  GTKCAIRO_LIBS="$GTKCAIRO_LIBS $GL_LIBS"
  use_gl=yes], [use_gl="no (bitrotted at the moment)"])
fi

dnl ===========================================================================

AC_OUTPUT([
gtkcairo.pc
Makefile
gtkcairo/Makefile
tests/Makefile
])

